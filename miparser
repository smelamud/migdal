#!/usr/bin/perl

# @(#) $Id$

$Line='';
$LineN=1;
$Eof=0;
$Token='';

%Quotes=('<!--' => 'Comment',
         '<?'   => 'PHP',
	 '<<'   => 'Remark',
         '<'    => 'Tag',
	 ''     => 'Text');
@GetPrefixes=('get','is','has');

@Requests=();
$Header='';
$Body='';
$Footer='';

$BodyToHeader=0;

$ElementName='';
$ElementBody='';
@ElementGlobals=();

$LastIter='';
$LastItem='';
$LastResult='';
$LastConst='';

$Stack='';

%Macros=();
%Elements=();

#### Utility functions ####

sub constructRemake
{
my ($s)=@_;

@del=();
@subs=();
%params=();
request('lib/uri.php');
getGlobal('REQUEST_URI');
splitParams($s,\%params);
while(my($name,$value)=each(%params))
     {
     if($value eq '')
       {
       push @del,"'$name'";
       }
     else
       {
       push @subs,"'$name' => ".subVar($value);
       }
     }
return 'remakeURI($REQUEST_URI,array('.join(',',@del).'),array('
                                      .join(',',@subs).'))';
}

sub getGetName
{
my ($name)=@_;

foreach(@GetPrefixes)
       {
       return $name if lc(substr($name,0,length $_)) eq $_;
       }
return "get$name";
}

sub getGlobal
{
my ($name)=@_;

push @ElementGlobals,$name if $ElementName ne '' and
                              !grep {$_ eq $name} @ElementGlobals;
return $name;
}

sub subVar
{
my ($s,$st,$ed)=@_;

my $item=$Stack->top()->getItem();
my $iter=$Stack->top()->getIterator();
my $result=$Stack->top()->getResult();
my $const=$Stack->top()->getConst();

if($s=~/^(.*)@([ERJ]?\w)$/)
  {
  request('lib/calendar.php');
  return "${st}formatAnyDate('$2',".subVar($1).")$ed";
  }
request('lib/utils.php') if $s=~/^\$\$?\^/;
return $st.constructRemake($1).$ed
       if $s=~/^\$\[([^\]]*)\]$/;
return "${st}urlencode(".constructRemake($1).")$ed"
       if $s=~/^\$#\[([^\]]*)\]$/;
return "$st$1$ed"
       if $s=~/^\*{?(\w+)}?$/;
return "${st}urlencode(\$".getGlobal('REQUEST_URI').")$ed"
       if $s=~/^\$_$/;
return "$st\$".getGlobal('REQUEST_URI').$ed
       if $s=~/^\$-$/;
return "$st\$".getGlobal('SCRIPT_NAME').$ed
       if $s=~/^\$~$/;
return "$st\$$item$ed"
       if $s=~/^\$\.$/;
return "${st}uc(\$$item)$ed"
       if $s=~/^\$\^\.$/;
return "$st\$$result$ed"
       if $s=~/^\$,$/;
return "${st}uc(\$$result)$ed"
       if $s=~/^\$\^,$/;
return "$st\$$iter$ed"
       if $s=~/^\$;$/;
return "$st\$$const$ed"
       if $s=~/^\$\*$/;
return "$st\$$1$ed"
       if $s=~/^\${?(\w+)}?$/;
return "${st}uc(\$$1)$ed"
       if $s=~/^\$\^{?(\w+)}?$/;
return "$st\$".getGlobal($1).$ed
       if $s=~/^\$\${?(\w+)}?$/;
return "${st}uc(\$".getGlobal($1).")$ed"
       if $s=~/^\$\$\^{?(\w+)}?$/;
return "${st}urlencode(\$$1)$ed"
       if $s=~/^\$#{?(\w+)}?$/;
return "${st}urlencode(\$".getGlobal($1).")$ed"
       if $s=~/^\$\$#{?(\w+)}?$/;
return "$st\$$item->".getGetName($1)."()$ed"
       if $s=~/^\${?\.(\w+)}?$/;
return "$st\$$result->".getGetName($1)."()$ed"
       if $s=~/^\${?,(\w+)}?$/;
return "$st\$$iter->".getGetName($1)."()$ed"
       if $s=~/^\${?;(\w+)}?$/;
return "$st\$$1->".getGetName($2)."()$ed"
       if $s=~/^\${?(\w+)[\.,;](\w+)}?$/;
return "$st\$".getGlobal($1).'->'.getGetName($2)."()$ed"
       if $s=~/^\$\${?(\w+)[\.,;](\w+)}?$/;
return "${st}uc(\$$item->".getGetName($1)."())$ed"
       if $s=~/^\$\^{?\.(\w+)}?$/;
return "${st}uc(\$$result->".getGetName($1)."())$ed"
       if $s=~/^\$\^{?,(\w+)}?$/;
return "${st}uc(\$$iter->".getGetName($1)."())$ed"
       if $s=~/^\$\^{?;(\w+)}?$/;
return "${st}uc(\$$1->".getGetName($2)."())$ed"
       if $s=~/^\$\^{?(\w+)[\.,;](\w+)}?$/;
return "${st}uc(\$".getGlobal($1).'->'.getGetName($2)."())$ed"
       if $s=~/^\$\$\^{?(\w+)[\.,;](\w+)}?$/;
return "${st}urlencode(\$$item->".getGetName($1)."())$ed"
       if $s=~/^\$#{?\.(\w+)}?$/;
return "${st}urlencode(\$$result->".getGetName($1)."())$ed"
       if $s=~/^\$#{?,(\w+)}?$/;
return "${st}urlencode(\$$iter->".getGetName($1)."())$ed"
       if $s=~/^\$#{?;(\w+)}?$/;
return "${st}urlencode(\$$1->".getGetName($2)."())$ed"
       if $s=~/^\$#{?(\w+)[\.,;](\w+)}?$/;
return "${st}urlencode(\$".getGlobal($1).'->'.getGetName($2)."())$ed"
       if $s=~/^\$\$#{?(\w+)[\.,;](\w+)}?$/;
return "'$st$s$ed'";
}

sub subVars
{
my ($s,$st,$ed)=@_;
my $c='';

while($s ne '')
     {
     my $head,$var;
     ($head,$var,$s)=$s=~m/
                           ^([^\$\*]*)
			   (
			    (?:
			     (?:
			      (?:
			       \$\$?[\#\^]?
			       (?:
				(?: {? \w* [\.,;]? \w+ }? )
				|
				(?: \[[^\]]*\] )
				|
				[_\.,;\~\*\-]
			       )
			      )
			      |
			      (?: \*{?\w+}? )
			     )
			     (?: @[ERJ]?\w )?
			    )?
			   )
			   (.*)$
			  /sx;
     $c.=$head;
     $c.=subVar($var,$st,$ed) if $var ne '';
     if($head eq '' && $var eq '')
       {
       $c.=substr($s,0,1);
       substr($s,0,1)='';
       }
     }
return $c;
}

sub subVarsText
{
my ($s)=@_;

return subVars($s,'<?php echo ','; ?>');
}

sub subMacroParam
{
my ($s,$params)=@_;

return $params->{$1}
       if $s=~/^%(\w+)$/;
return uc($params->{$1})
       if $s=~/^%\^(\w+)$/;
return $params->{$1}
       if $s=~/^%{(\w+)}$/;
return uc($params->{$1})
       if $s=~/^%\^{(\w+)}$/;
return $s;
}

sub subMacroParams
{
my ($s,$params)=@_;
my $c='';

while($s ne '')
     {
     my $head,$var;
     ($head,$var,$s)=$s=~/^([^%]*)((?:%[\^]?{?\w+}?)?)(.*)$/s;
     $c.=$head;
     $c.=subMacroParam($var,$params) if $var ne '';
     }
return $c;
}

sub splitParams
{
my ($token,$args)=@_;

while($token!~/^\s*$/)
     {
     my $name,$value;
     ($name,$value,$token)=$token=~m/
                                     ^\s*(\w+)\s*
				     (?:
                                      =\s*
				      (
				       (?: [^\s'"]+ )
				       |
				       (?: '[^']*' )
				       |
				       (?: "[^"]*" )
				      )
				     )?
				     (.*)$
				    /sx;
     $value=substr $value,1,-1 if $value=~/^['"]/;
     $args->{lc $name}=$value;
     }
}

sub constructParams
{
my ($args)=@_;

my @params=();
for(my $n=1;exists $args->{"arg$n"};$n++)
   {
   push @params,subVar($args->{"arg$n"});
   }
return join ',',@params;
}

#### Macro ####

package Macro;

sub new
{
my ($class,$body,$defaults)=@_;

my $self=bless {'body' => $body},$class;
$self->{'defaults'}={};
::splitParams($defaults,$self->{'defaults'});
return $self;
}

sub getBody
{
my ($self)=@_;

return $self->{'body'};
}

sub getInstance
{
my ($self,$params)=@_;

my %args=();
while(my($name,$value)=each(%{$self->{'defaults'}}))
     {
     $args{$name}=$value;
     }
while(my($name,$value)=each(%{$params}))
     {
     $args{$name}=$value if $value ne '';
     }
my $s=::subMacroParams($self->getBody(),\%args);
return $s;
}

#### Element ####

package Element;

sub new
{
my ($class,$file,$params,$defaults)=@_;

my $self=bless {'file' => $file},$class;
$params=~s/^\s+//;
$params=~s/\s+$//;
$self->{'params'}=[];
@{$self->{'params'}}=map{s/^\$//;$_} split(/[\s,]+/,$params);
$self->{'defaults'}={};
::splitParams($defaults,$self->{'defaults'});
return $self;
}

sub getFile
{
my ($self)=@_;

return $self->{'file'};
}

sub getParams
{
my ($self,$args)=@_;

my @list=();
foreach(@{$self->{'params'}})
       {
       push @list,::subVar(exists $args->{$_} && $args->{$_} ne ''
                           ? $args->{$_} : $self->{'defaults'}->{$_});
       }
return join ',',@list;
}

#### StackFrame ####

package StackFrame;

sub new
{
my ($class,$frame)=@_;

my $data={'iterator' => '',
          'item'     => '',
	  'result'   => '',
	  'const'    => ''};
if(defined($frame))
  {
  while(my($name,$value)=each(%{$frame}))
       {
       $data->{$name}=$value;
       }
  }
return bless $data,$class;
}

sub getIterator
{
my ($self)=@_;

return $self->{'iterator'};
}

sub setIterator
{
my ($self,$value)=@_;

return $self->{'iterator'}=$value;
}

sub getItem
{
my ($self)=@_;

return $self->{'item'};
}

sub setItem
{
my ($self,$value)=@_;

return $self->{'item'}=$value;
}

sub getResult
{
my ($self)=@_;

return $self->{'result'};
}

sub setResult
{
my ($self,$value)=@_;

return $self->{'result'}=$value;
}

sub getConst
{
my ($self)=@_;

return $self->{'const'};
}

sub setConst
{
my ($self,$value)=@_;

return $self->{'const'}=$value;
}

#### Stack ####

package Stack;

sub new
{
my ($class)=@_;

$self=bless {'stack' => []},$class;
$self->push();
return $self;
}

sub top
{
my ($self)=@_;

return ${$self->{'stack'}}[$#{$self->{'stack'}}];
}

sub pop
{
my ($self)=@_;

return pop @{$self->{'stack'}};
}

sub push
{
my ($self)=@_;

return push @{$self->{'stack'}},StackFrame->new($self->top());
}

#### Token ####

package Token;

sub new
{
my ($class,$n)=@_;

return bless {'token' => ::drop($n)},$class;
}

sub getToken
{
my ($self)=@_;

return $self->{'token'};
}

sub process
{
my ($self)=@_;

::body($self->getToken());
}

#### Text ####

package Text;

@ISA=qw(Token);

sub new
{
my ($class)=@_;

my $i=index $::Line,'<';
$i=$i<0 ? length($::Line) : $i;
return Token::new($class,$i);
}

sub process
{
my ($self)=@_;

::body(::subVarsText($self->getToken()));
}

#### QuotedToken ####

package QuotedToken;

@ISA=qw(Token);

sub new
{
my ($class,$quote)=@_;

my $i;
::more() while(($i=index($::Line,$quote))<0 && !$::Eof);
$::Line.=$quote if $i<0;
$i=index($::Line,$quote)+length($quote);
return Token::new($class,$i);
}

#### Tag ####

package Tag;

@ISA=qw(Token);

sub new
{
my ($class)=@_;

::more() while($::Line !~ /^<(([^>'"]*)|('[^']*')|("[^"]*"))*>/s && !$::Eof);
my ($token)=$::Line =~ /^(<(([^>'"]*)|('[^']*')|("[^"]*"))*>)/s;
my $self=Token::new($class,length($token));
$token=substr $token,1,-1;
($self->{'name'},$token)=$token=~/^\s*(\/?\w+)(.*)$/s;
$self->{'name'}=lc $self->{'name'};
$self->{'args'}={};
::splitParams($token,$self->{'args'});
return $self;
}

sub getName
{
my ($self)=@_;

return $self->{'name'};
}

sub getArgs
{
my ($self)=@_;

return $self->{'args'};
}

sub getArg
{
my ($self,$name)=@_;

return $self->{'args'}->{$name};
}

sub process
{
my ($self)=@_;

my $tagName=lc $self->getName();
my $name=$tagName;
$name=~tr/\//_/;
$name='el'.ucfirst($name);
if($self->can($name))
  {
  &$name($self);
  }
elsif(defined($::Macros{$tagName}))
  {
  $::Line=$::Macros{$self->getName()}->getInstance($self->getArgs()).$::Line;
  }
elsif(defined($::Elements{$tagName}))
  {
  ::request($::Elements{$tagName}->getFile());
  ::body('<?php display',$self->getName(),'(',
         $::Elements{$tagName}->getParams($self->getArgs()),'); ?>');
  }
else
  {
  ::body(::subVarsText($self->getToken()));
  }
}

sub elAsp
{
::body('<?php ?>');
}

sub elRefresh #XXX Possibly empty period,url
{
my ($self)=@_;

my $period=::subVar($self->getArg('period'));
my $url=::subVar($self->getArg('url'));
::header("<?php header('Refresh: '.$period.'; URL='.$url); ?>\n");
::body("<meta http-equiv='Refresh' value='<?php echo $period
                                           ?>;URL=<?php echo $url ?>'>\n");
}

sub elHtml
{
my ($self)=@_;

::request('lib/errorreporting.php');
::request('lib/database.php');
::request('lib/session.php');
::request('lib/no-cache.php');
::header(<<EOT);
<?php
noCacheHeaders();
dbOpen();
session();
?>
EOT
::body(<<EOT);
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN">
<?php noCacheMeta() ?>
EOT
::body($self->getToken());
}

sub el_html
{
my ($self)=@_;

::footer(<<EOT);
<?php
dbClose();
?>
EOT
::body($self->getToken());
}

sub elHead
{
my ($self)=@_;

::request('lib/style.php');
::body(<<EOT);
<head>
<link rel='stylesheet' href='/styles/style-<?php echo getStyle(\$userStyle) ?>.css'>
<?php include('styles/style-'.getStyle(\$userStyle).'.php') ?>
EOT
}

sub elRequire
{
my ($self)=@_;

::request($self->getArg('name'));
}

sub elInclude
{
my ($self)=@_;

::body('<?php include(',::subVar($self->getArg('name')),'); ?>');
}

sub pushArray
{
($self,$array,$name)=@_;

if(exists $self->getArgs()->{$name})
  {
  my $value=::subVar($self->getArg($name));
  if($value=~/^'([^']*)'$/)
    {
    $value=~s/^'([^']*)'$/\1/;
    push(@$array,map("'$_'",split(/[\s,]+/,$value)));
    }
  else
    {
    push @$array,$value;
    }
  }
}

sub elArray
{
my ($self)=@_;

my $name=$self->getArg('name');
$name='const'.($::LastConst++) if $name eq '';
::body("<?php \$$name=array(");
my @vals=();
$self->pushArray(\@vals,'vals');
for(my $n=1;exists $self->getArgs()->{"val$n"};$n++)
   {
   $self->pushArray(\@vals,"val$n");
   }
::body(join(',',@vals),'); ?>');
$::Stack->top()->setConst($name);
}

sub elAssign #XXX Possibly empty value
{
my ($self)=@_;

my $name=$self->getArg('name');
$name='const'.($::LastConst++) if $name eq '';
::body("<?php \$$name=",::subVar($self->getArg('value')),'; ?>');
$::Stack->top()->setConst($name);
}

sub elIterate #XXX Possibly empty class
{
my ($self)=@_;

my $name=$self->getArg('name');
$name='iter'.($::LastIter++) if $name eq '';
::body("<?php \$$name=new ",$self->getArg('class'),
       '(',::constructParams($self->getArgs()),'); ?>');
$::Stack->top()->setIterator($name);
}

sub elFor #XXX Possibly empty list
{
my ($self)=@_;

my $list=$self->getArg('list');
$list=$::Stack->top()->getIterator() if $list eq '';
my $name=$self->getArg('name');
$name='item'.($::LastItem++) if $name eq '';
::body("<?php while(\$$name=\$$list->next()) { ?>");
$::Stack->push();
$::Stack->top()->setItem($name);
}

sub el_for #XXX Possibly incorrect closure
{
my ($self)=@_;

$::Stack->pop();
::body("<?php } ?>");
}

sub elMacro #XXX Possibly empty name
{
my ($self)=@_;

my $body='';
::pullToken();
while((ref($::Token) ne 'Tag' || $::Token->getName() ne '/macro')
      && $::Line ne '')
     {
     $body.=$::Token->getToken();
     ::pullToken();
     }
$::Macros{lc $self->getArg('name')}=Macro->new($body,$self->getArg('defaults'));
}

sub elIf
{
my ($self)=@_;

::body('<?php if(',::subVars($self->getArg('what')),') { ?>');
}

sub elElse #XXX Possibly incorrect closure
{
my ($self)=@_;

::body('<?php } else { ?>');
}

sub el_if #XXX Possibly incorrect closure
{
my ($self)=@_;

::body('<?php } ?>');
}

sub elObj #XXX Possibly empty class
{
my ($self)=@_;

my $name=$self->getArg('name');
$name='result'.($::LastResult++) if $name eq '';
::body("<?php \$$name=new ",$self->getArg('class'),
       '(',::constructParams($self->getArgs()),'); ?>');
$::Stack->top()->setResult($name);
}

sub elCall #XXX Possibly empty func
{
my ($self)=@_;

my $name=$self->getArg('name');
$name='result'.($::LastResult++) if $name eq '';
::body("<?php \$$name=",$self->getArg('func'),
       '(',::constructParams($self->getArgs()),'); ?>');
$::Stack->top()->setResult($name);
}

sub elHeader
{
my ($self)=@_;

$::BodyToHeader=1;
}

sub el_header
{
my ($self)=@_;

$::BodyToHeader=0;
}

sub elDecel #XXX Possibly empty name
{
my ($self)=@_;

$::Elements{lc $self->getArg('name')}=Element->new($self->getArg('file'),
                                                   $self->getArg('params'),
                                                   $self->getArg('defaults'));
}

sub elElement #XXX Possibly empty name or incorrect closure
{
my ($self)=@_;

$::ElementName=lc $self->getArg('name');
$::ElementBody='';
@::ElementGlobals=();
if(!exists $::Elements{$::ElementName})
  {
  $::Elements{$::ElementName}=Element->new('',$self->getArg('params'),
					   $self->getArg('defaults'));
  }
$::Stack->push();
}

sub el_element #XXX Possibly incorrect closure
{
my ($self)=@_;

$::Stack->pop();
my $name=$::ElementName;
$::ElementName='';
my @params=map "\$$_",@{$::Elements{$name}->{'params'}};
my $globals='';
$globals="global ".join(',',map {"\$$_"} @::ElementGlobals).";\n"
         if scalar(@::ElementGlobals)>0;
::body("<?php\nfunction display$name(",join(',',@params),")\n{\n",
       $globals,"?>\n",$::ElementBody,"<?php\n}\n?>\n");
}

sub elSettype #XXX Possibly empty name,type or unknown type
{
my ($self)=@_;

my $name=$self->getArg('name');
my $type=$self->getArg('type');
my $method=$self->getArg('method');
$method=$method eq '' ? 'GET' : uc $method;
::body(<<EOT);
<?php
settype(\$$name,'$type');
settype(\$HTTP_${method}_VARS['$name'],'$type');
?>
EOT
}

sub elSetup
{
my ($self)=@_;

my $name=$self->getArg('name');
$name=$::Stack->top()->getResult() if $name eq '';
if($self->getArg('var') eq '')
  {
  my $method=$self->getArg('method');
  $method='get' if $method eq '';
  $method=uc $method;
  ::body("<?php \$$name->setup(\$",::getGlobal("HTTP_${method}_VARS"),"); ?>");
  }
else
  {
  ::body("<?php \$$name->set".$self->getArg('var').
         '('.::subVar($self->getArg('value')).'); ?>');
  }
}

sub elZone
{
my ($self)=@_;

$::Stack->push();
}

sub el_zone
{
my ($self)=@_;

$::Stack->pop();
}

#### Comment ####

package Comment;

@ISA=qw(QuotedToken);

sub new
{
my ($class)=@_;

return QuotedToken::new($class,'-->');
}

#### Remark ####

package Remark;

@ISA=qw(QuotedToken);

sub new
{
my ($class)=@_;

return QuotedToken::new($class,'>>');
}

sub process
{
}

#### PHP ####

package PHP;

@ISA=qw(QuotedToken);

sub new
{
my ($class)=@_;

return QuotedToken::new($class,'?>');
}

#### main ####

package main;

use Getopt::Long;
Getopt::Long::Configure('no_auto_abbrev','no_ignore_case','bundling');

$VERSION='0.0.1';

sub more
{
return if $Eof;
my $s=<>;
if(defined($s))
  {
  $Line.=$s;
  }
else
  {
  $Eof=1;
  }
}

sub pull
{
more() while(!$Eof && $Line eq '');
}

sub drop
{
my $n=shift;

my $s=substr($Line,0,$n);
$LineN+=$s=~/\n/;
substr($Line,0,$n)='';
pull;
return $s;
}

sub pullToken
{
my $quote='';
my $class='';
while(my($q,$c)=each(%Quotes))
     {
     if(substr($Line,0,length($q)) eq $q && length($q)>=length($quote))
       {
       $quote=$q;
       $class=$c;
       }
     }
my $name=$class.'::new';
return $Token=&$name($class);
}

sub request
{
my ($name)=@_;

push @Requests,$name unless $name eq '' or grep {$_ eq $name} @Requests;
}

sub printVar
{
$var=shift;
$data=join '',@_;
if($$var=~/\n$/ || $$var eq '')
  {
  $data=~s/^\s+//;
  }
else
  {
  $data=~s/^\s*\n\s*/\n/;
  }
$$var.=$data;
$$var=~s/\s\?>\s*\n\s*<\?php\s/\n/sg;
$$var=~s/\s\?>\s*<\?php\s//sg;
}

sub header
{
printVar(\$Header,@_);
}

sub body
{
if($BodyToHeader)
  {
  header(@_);
  }
elsif($ElementName ne '')
  {
  printVar(\$ElementBody,@_);
  }
else
  {
  printVar(\$Body,@_);
  }
}

sub footer
{
printVar(\$Footer,@_);
}

sub display
{
print STDERR @_;
}

sub help
{
print <<EOF;
Usage: $0 [<options>] [<file>]

Migdal website kernel MHTML macro language preprocessor

  -h, --help                Print this help and exit
  -M, --macros=<filename>   Set macro file path (default is `macros.mhtml')
  -v, --version             Print version information and exit

Report bugs to <balu\@yes.ru>
EOF
exit;
}

sub version
{
print <<EOF;
miparser (migdal $VERSION)
Copyright (C) 2001 by Dmitry E. Melamud.

This program is free software; you can redistribute it
and/or modify it under the terms of the GNU General Public
License (see LICENSE).

EOF
exit;
}

sub macros
{
my (undef,$macros)=@_;

unshift @ARGV,$macros if -r $macros;
}

sub init
{
$|=1;
GetOptions('help|h|?'	=> \&help,
           'macros|M=s'	=> \&macros,
           'version|v'	=> \&version);
$Stack=Stack->new();
pull;
}

sub run
{
while($Line ne '')
     {
     pullToken;
     $Token->process();
     }
}

sub done
{
my $s="<?php\n";
foreach(@Requests)
       {
       $s.="require_once('$_');\n";
       }
$s.="?>\n";
$s=$s.$Header.$Body.$Footer;
$s=~s/\s\?>\s*\n\s*<\?php\s/\n/sg;
$s=~s/\s\?>\s*<\?php\s//sg;
print $s;
}

init;
run;
done;
